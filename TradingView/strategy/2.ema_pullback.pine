//@version = 5
strategy("Best Pullback Trading Strategy", overlay = true, initial_capital = 1000, default_qty_type = strategy.percent_of_equity, default_qty_value = 100)

// Youtuber: Financial Programming with Ritvik, CFA
// Reference: https://www.youtube.com/watch?v=mHU4iqcRaQ8

// Inputs 
ma_length1 = input.int(280, 'MA length 1', step = 10, group = 'Moving Avg. Params', inline = 'MA')
ma_length2 = input.int(13, 'MA length 2', step = 1, group = 'Moving Avg. Params', inline = 'MA')
sl = input.float(title = 'Stop Loss (%)', defval = 0.07, step = 0.1, group = 'Moving Avg. Params')
too_deep = input.float(title = 'Too Deep (%)', defval = 0.27, step = 0.01, group = 'Too Deep and Thin conditions', inline = 'Too')
too_thin = input.float(title = 'Too Thin (%)', defval = 0.03, step = 0.01, group = 'Too Deep and Thin conditions', inline = 'Too')

// Calculations
ma1 = ta.sma(close, ma_length1)
ma2 = ta.sma(close, ma_length2)
too_deep2 = (ma2/ma1-1) < too_deep
too_thin2 = (ma2/ma1-1) > too_thin

// Entry and close conditions
var float buy_price = 0
buy_condition = (close > ma1) and (close < ma2) and strategy.position_size == 0 and too_deep2 and too_thin2
close_condition1 = (close > ma2) and strategy.position_size > 0 and (close < low[1])
stop_distance = strategy.position_size > 0 ? ((buy_price - close) / close) : na 
close_condition2 = strategy.position_size > 0 and stop_distance > sl
stop_price = strategy.position_size > 0 ? buy_price - (buy_price * sl) : na 

// Entry and close orders
if buy_condition 
    strategy.entry('Long', strategy.long)
if buy_condition[1]
    buy_price := open 
if close_condition1 or close_condition2
    strategy.close('Long', comment = 'Exit' + (close_condition2 ? 'SL=true' : ''))
    buy_price := na 


plot(ma1, color = color.blue)
plot(ma2, color = color.orange)
